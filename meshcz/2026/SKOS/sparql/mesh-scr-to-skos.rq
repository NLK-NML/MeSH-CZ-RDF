### Conversion of MeSH *SCR* to SKOS format
##  - version 1.0.7 2026-01-27
## Run the query - using either method:
# a. Copy this file to Apache Jena data dir, STOP Fuseki instance, and run:
#  tdb2_tdbquery --loc=databases/mesh --query=mesh-scr-to-skos.rq --time > mesh-skos-scr.ttl
##  Or
# b. Copy this file to a dir where the MeSH nq gzipped dataset is located and run:
#  arq --query=mesh-scr-to-skos.rq --data=meshcz.nq.gz --time > mesh-skos-scr.ttl
#
PREFIX mesh:  <http://id.nlm.nih.gov/mesh/>
PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
PREFIX mesht: <http://www.medvik.cz/schema/mesh/vocab/#>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:  <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
  ?d skos:hiddenLabel ?plabel .
  ?d skos:hiddenLabel ?alabel .
  ?d skos:hiddenLabel ?tlabel .
  ?d skos:hiddenLabel ?talabel .
}
WHERE {
  VALUES ?type {
    meshv:SCR_Anatomy
	meshv:SCR_Disease
	meshv:SCR_Chemical
	meshv:SCR_Organism
	meshv:SCR_Population
	meshv:SCR_Protocol
  }
  ?sc rdf:type ?type .
  FILTER NOT EXISTS { ?sc meshv:active false }
  ?sc meshv:preferredConcept ?c .
  ?sc meshv:preferredMappedTo ?d .
  FILTER NOT EXISTS { ?d meshv:active false }
  FILTER NOT EXISTS { ?c meshv:active false }

  ?c meshv:preferredTerm ?pt .
  ?pt meshv:prefLabel ?plabel .

  OPTIONAL { ?pt meshv:altLabel ?alabel }

  OPTIONAL {   ?c meshv:term ?ptt .
                ?ptt meshv:prefLabel ?tlabel }

  OPTIONAL { ?c meshv:term ?ptt .
              ?pt meshv:altLabel ?talabel }
}
#LIMIT 10000
